# ====== Rules for aligning reads with bowtie2 =================================


# Align trimmed reads with bowtie2
rule bowtie:
    input:
        R1  = PROJ + "/bbduk/{sample}_R1_bbduk.fastq.gz",
        R2  = PROJ + "/bbduk/{sample}_R2_bbduk.fastq.gz"
    output:
        bam   = PROJ      + "/bams/{sample}_" + INDEX_SAMPLE + "_" + SEQ_DATE + ".bam",
        bai   = PROJ      + "/bams/{sample}_" + INDEX_SAMPLE + "_" + SEQ_DATE + ".bam.bai",
        bamt  = temp(PROJ + "/bams/{sample}_temp.bam"),
        bait  = temp(PROJ + "/bams/{sample}_temp2.bam"),
        stats = PROJ      + "/bams/{sample}_bowtie_stats.txt",
        reads = PROJ      + "/bams/{sample}_count.txt"
    params:
        job_name = "{sample}_bowtie",
        memory   = 64,
        idx      = INDEX_PATH + INDEX_SAMPLE,
        idx2     = INDEX_PATH + INDEX_SAMPLE + ".txt",
        mask     = MASK,
        sortname = "{sample}.temp",
        enrich   = ENRICHED,
        args     = CMD_PARAMS["bowtie2"]
    log:
        out = PROJ + "/logs/{sample}_bowtie.out",
        err = PROJ + "/logs/{sample}_bowtie.err"
    message:
        "Aligning reads with Bowtie2 for {wildcards.sample}"
    threads: 
        12
    shell:
        """
        # By default bowtie2 performs end-to-end alignment
        # By default bowtie2 searches for multiple alignments and reports the best one
        bowtie2 \
            -p {threads} \
            -x {params.idx} \
            -1 {input.R1} \
            -2 {input.R2} \
            {params.args} \
            2> {output.stats} \
            | samtools view -bF4q5 - \
            | samtools view -L {params.mask} -b - -U {output.bamt} > {output.bait}
        
        # remove rRNA reads
          samtools view -b {output.bamt} $(head -n1 {params.idx2}) -@ {threads} \
          | samtools sort - -T {params.sortname} -@ {threads} -O bam \
          > {output.bam}
          samtools index -@ {threads} {output.bam}
      
        echo "{INDEX_SAMPLE} $(samtools idxstats {output.bam} | awk '{{s+=$3}} END {{print s/2}}')" >> {output.reads}
        echo "enrich_{INDEX_SAMPLE} $(samtools view {output.bam} -L {params.enrich} -c | awk ' {{print $0/2}}')" >> {output.reads}
        echo "chrM_{INDEX_SAMPLE} $(samtools view {output.bam} chrM -c | awk ' {{print $0/2}}')" >> {output.reads}
        
        """

# fragment size of mapped paired end reads
rule fragment_size:
    input:
      bam = expand(
            PROJ + "/bams/{sample}_" + INDEX_SAMPLE + "_" + SEQ_DATE + ".bam",
            sample = SAMS_UNIQ2
        ),
      bai =  expand(
            PROJ + "/bams/{sample}_" + INDEX_SAMPLE + "_" + SEQ_DATE + ".bam.bai",
            sample = SAMS_UNIQ2
        )
    output:
      log =  PROJ + "/stats/" + PROJ + "_" + INDEX_SAMPLE + "_fragment.txt",
      pic =  PROJ + "/stats/" + PROJ + "_" + INDEX_SAMPLE + "_fragmentSize.png"
    params:
        job_name = "PE_fragment_size",
        memory   = 64,
        labels   = SAMS_UNIQ2,
        args     = CMD_PARAMS["fragment_size"]
    log:
        out = PROJ + "/logs/PE_fragment_size.out",
        err = PROJ + "/logs/PE_fragment_size.err"
    message:
        "fragment size of mapped paired end reads"
    threads: 
        12
    shell:
        """
        bamPEFragmentSize \
          -hist {output.pic} \
          --numberOfProcessors {threads} \
          -T "Fragment size of PE data" \
          -b  {input.bam} {params.args} \
          --samplesLabel {params.labels} \
          --table {output.log}
    
        """

# Combine bowtie2 summaries
rule bowtie_summary:
    input:
        expand(
            PROJ + "/bams/{sample}_bowtie_stats.txt",
            sample = SAMS_UNIQ
        )
    output:
        PROJ + "/stats/" + PROJ + "_aligned.tsv"
    params:
        job_name = PROJ + "_bowtie_summary",
        memory   = 4
    log:
        out = PROJ + "/logs/" + PROJ + "_bowtie_summary.out",
        err = PROJ + "/logs/" + PROJ + "_bowtie_summary.err"
    message:
        "Creating " + PROJ + " bowtie2 summary"
    threads:
        1
    run:
        with open(output[0], "w") as out:
            for file in input:
                name = os.path.basename(file)
                name = re.sub("_bowtie_stats.txt", "", name)

                for line in open(file, "r"):
                    match = re.search("overall alignment rate", line)

                    if match:
                        num = re.search("[0-9.%]+", line)
                        num = num.group(0)
                        met = re.search("[a-z\s]+", line).group(0)
                        met = met.strip()
                        met = re.sub(" ", "_", met)

                        out.write("%s\t%s\t%s\n" % (name, met, num))


# make URLS and copy files to sandbox
rule my_bowtie_sample_url:
    input:
        bam   = PROJ + "/bams/{sample}_" + INDEX_SAMPLE + "_" + SEQ_DATE + ".bam",
        bai   = PROJ + "/bams/{sample}_" + INDEX_SAMPLE + "_" + SEQ_DATE + ".bam.bai",
    output:
        temp(PROJ + "/bams/{sample}_" + INDEX_SAMPLE + "_url.txt")
    params:
        job_name = PROJ + "_bowtie_sample_url",
        memory   = 4,
        url      = "http://amc-sandbox.ucdenver.edu/" + USER + "/" + SEQ_DATE + "_" + PROJ + "/{sample}_" + INDEX_SAMPLE + "_" + SEQ_DATE + ".bam"
    log:
        out = PROJ + "/logs/{sample}_" + PROJ + "_bowtie_sample_url.out",
        err = PROJ + "/logs/{sample}_" + PROJ + "_bowtie_sample_url.err"
    message:
        "Creating " + PROJ + " bowtie_sample url"
    threads:
        1
    shell:
        """
          echo "{params.url}" >> {output}
          ssh amc-sandbox 'mkdir -p ./public_html/{SEQ_DATE}_{PROJ}'
          scp {input.bam}* amc-sandbox:./public_html/{SEQ_DATE}_{PROJ}
          
        """
        
# Combine bowtie2 urls
rule bowtie_sample_urls:
    input:
        expand(
            PROJ + "/bams/{sample}_" + INDEX_SAMPLE + "_url.txt",
            sample = SAMS_UNIQ
        )
    output:
        PROJ + "/URLS/" + PROJ + "_" + INDEX_SAMPLE + "_bam_URL.txt"
    params:
        job_name = PROJ + "_bowtie_url",
        memory   = 4
    log:
        out = PROJ + "/logs/" + PROJ + "_bowtie_sample_url.out",
        err = PROJ + "/logs/" + PROJ + "_bowtie_sample_url.err"
    message:
        "Creating " + PROJ + " bowtie2 url"
    threads:
        1
    run:
        with open(output[0], "w") as out:
            for file in input:
                for line in open(file, "r"):
                    out.write(line)
                    
                    