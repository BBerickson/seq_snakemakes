# ====== Rules for making standard sense 5L matrix files =================================


def _get_col(wildcards):
    sample = SAMPLES[wildcards.group][0]
  
    if sample in COLS_DICT:
      results = (COLS_DICT[sample])
    else:
      results = "0,0,0"
    return(results)
          

# stranded sense matrix files
rule my_5L_matrix:
    input:
        bw  =   PROJ   + "/bw/{group}_" + INDEX_SAMPLE + "_" + SEQ_DATE + "_" + NORMS + ".bw"
    output:
        matrix = temp(PROJ   + "/matrix/{group}_temp0_5L_matrix.gz")
    params:
        job_name = "{group}_matrix",
        memory   = 20,
        args     = CMD_PARAMS["long5"],
        my_ref   = MY_REF
    log:
        out = PROJ + "/logs/{group}_5L_matrix.out",
        err = PROJ + "/logs/{group}_5L_matrix.err"
    message:
        "5L matrix files {wildcards.group}"
    threads: 
        12
    shell:
        """
        # 
        computeMatrix reference-point \
          {params.args} \
          -R {params.my_ref} \
          -S {input.bw} \
          -p {threads} \
          -o {output.matrix}

        """
        
# stranded sense matrix files
rule fix_5L_matrix:
    input:
        PROJ   + "/matrix/{group}_temp0_5L_matrix.gz"
    output:
        PROJ + "/matrix/5L_{group}_" + INDEX_SAMPLE + "_" + SEQ_DATE + "_" + NORM5L + "_matrix.gz"
    params:
        job_name = "{group}_sense_matrix",
        memory   = 20,
        args    = CMD_PARAMS["table5L"]
    log:
        out = PROJ + "/logs/{group}_fix_5L_matrix.out",
        err = PROJ + "/logs/{group}_fix_5L_matrix.err"
    message:
        "fix standard 5L matrix files {wildcards.group}"
    threads: 
        1
    script:
        '../R_scripts/MatrixTrim.R'
        
# make url's and scp to sandbox
rule my_5L_url:
    input:
        PROJ + "/matrix/5L_{group}_" + INDEX_SAMPLE + "_" + SEQ_DATE + "_" + NORM5L + "_matrix.gz"
    output:
        temp(PROJ + "/matrix/5L.{group}_matrix.url.txt")
    params:
        job_name = PROJ + "_5L_matrix_url",
        memory   = 4,
        color    = _get_col,
        url_out  = "http://amc-sandbox.ucdenver.edu/" + USER + "/" + PROJ + "/5L_{group}_" + INDEX_SAMPLE + "_" + SEQ_DATE + "_" + NORM5L + "_matrix.gz"
    log:
        out = PROJ + "/logs/{group}_5L_matrix_url.out",
        err = PROJ + "/logs/{group}_5L_matrix_url.err"
    message:
        "Creating 5L_matrix url for {wildcards.group}"
    threads:
        1
    shell:
        """
        # fix R appended file remote read error
        computeMatrixOperations relabel -m {input} -o {input}
        echo {params.url_out} 5L 5L.{wildcards.group}_{SEQ_DATE} {params.color} >> {output[0]}
        ssh amc-sandbox 'mkdir -p ./public_html/{PROJ}'
        scp {input[0]} amc-sandbox:./public_html/{PROJ}
        
        """
        
# Combine urls
rule my_5L_url_summary:
    input:
        sorted(expand(
            PROJ + "/matrix/5L.{group}_matrix.url.txt",
            group = GRPS_UNIQ
        ))
    output:
        PROJ + "/URLS/5L_" + PROJ + "_" + INDEX_SAMPLE + "_" + NORM5L + "_matrix.url.txt"
    params:
        job_name = PROJ + "_5L_matrix_url",
        memory   = 4
    log:
        out = PROJ + "/logs/" + PROJ + "_5L_matrix_url.out",
        err = PROJ + "/logs/" + PROJ + "_5L_matrix_url.err"
    message:
        "Creating " + PROJ + " sense 5L_matrix url"
    threads:
        1
    run:
        with open(output[0], "w") as out:
            for file in input:
                for line in open(file, "r"):
                    out.write(line)




