# ====== Rules for making standard sense 3 matrix files =================================


def _get_col(wildcards):
    sample = SAMPLES[wildcards.group][0]
  
    if sample in COLS_DICT:
      results = (COLS_DICT[sample])
    else:
      results = "0,0,0"
    return(results)
          

# stranded sense matrix files
rule my_3_sense_matrix:
    input:
        fw  =   PROJ   + "/bw/{group}_" + INDEX_SAMPLE + "_" + SEQ_DATE + "_" + NORMS + "_fw.bw",
        rev =   PROJ   + "/bw/{group}_" + INDEX_SAMPLE + "_" + SEQ_DATE + "_" + NORMS + "_rev.bw"
    output:
        sense   = temp(PROJ   + "/matrix/{group}_temp0_3_matrix.gz"),
        sense1  = temp(PROJ   + "/matrix/{group}_temp1_3_matrix.gz"),
        sense2  = temp(PROJ   + "/matrix/{group}_temp2_3_matrix.gz")
    params:
        job_name = "{group}_sense_matrix",
        memory   = 20,
        args     = CMD_PARAMS["sense3"],
        fw_ref   = FW_REF,
        rev_ref  = REV_REF
    log:
        out = PROJ + "/logs/{group}_sense_3_matrix.out",
        err = PROJ + "/logs/{group}_sense_3_matrix.err"
    message:
        "standard sense 3 matrix files {wildcards.group}"
    threads: 
        12
    shell:
        """
        # no unscaled body
        computeMatrix reference-point \
          {params.args} \
          -R {params.fw_ref} \
          -S {input.fw} \
          -p {threads} \
          -o {output.sense1}

        computeMatrix reference-point \
          {params.args} \
          -R {params.rev_ref} \
          -S {input.rev} \
          -p {threads} \
          -o {output.sense2}

        computeMatrixOperations rbind -m {output.sense1} {output.sense2} -o {output.sense}

        """

# stranded sense matrix files
rule fix_3_sense_matrix:
    input:
        PROJ   + "/matrix/{group}_temp0_3_matrix.gz"
    output:
        PROJ + "/matrix/3_{group}_" + INDEX_SAMPLE + "_" + SEQ_DATE + "_" + NORM3 + "_matrix.gz"
    params:
        job_name = "{group}_3_sense_matrix",
        memory   = 20,
        args    = CMD_PARAMS["table3"]
    log:
        out = PROJ + "/logs/{group}_fix_3_sense_matrix.out",
        err = PROJ + "/logs/{group}_fix_3_sense_matrix.err"
    message:
        "fix standard sense 3 matrix files {wildcards.group}"
    threads: 
        1
    script:
        '../R_scripts/MatrixTrim.R'
        

# make url's and scp to sandbox
rule my_3_url:
    input:
        matrix1 = PROJ + "/matrix/3_{group}_" + INDEX_SAMPLE + "_" + SEQ_DATE + "_" + NORM3 + "_matrix.gz"
    output:
        temp(PROJ + "/matrix/3.{group}_matrix.url.txt")
    params:
        job_name = PROJ + "_3_matrix_url",
        memory   = 4,
        color    = _get_col,
        url_out  = "http://amc-sandbox.ucdenver.edu/" + USER + "/" + PROJ + "/3_{group}_" + INDEX_SAMPLE + "_" + SEQ_DATE + "_" + NORM3 + "_matrix.gz",
        matrix1g = PROJ + "/matrix/3_{group}_" + INDEX_SAMPLE + "_" + SEQ_DATE + "_" + NORM3 + "_matrix"
    log:
        out = PROJ + "/logs/{group}_sense_3_matrix_url.out",
        err = PROJ + "/logs/{group}_sense_3_matrix_url.err"
    message:
        "Creating sense 3_matrix url for {wildcards.group}"
    threads:
        1
    shell:
        """
        # fix for strange side affect that only the 1 line loads from sandbox
        gunzip {input.matrix1}
        gzip {params.matrix1g}
        echo {params.url_out} 3 3.{wildcards.group}_{SEQ_DATE} {params.color} >> {output[0]}
        ssh amc-sandbox 'mkdir -p ./public_html/{PROJ}'
        scp {input.matrix1} amc-sandbox:./public_html/{PROJ}
        
        """
        
# Combine urls
rule my_3_url_summary:
    input:
        sorted(expand(
            PROJ + "/matrix/3.{group}_matrix.url.txt",
            group = GRPS_UNIQ
        ))
    output:
        PROJ + "/URLS/3_" + PROJ + "_" + INDEX_SAMPLE + "_" + NORM3 + "_sense_matrix.url.txt"
    params:
        job_name = PROJ + "_sense_3_matrix_url",
        memory   = 4
    log:
        out = PROJ + "/logs/" + PROJ + "_sense_3_matrix_url.out",
        err = PROJ + "/logs/" + PROJ + "_sense_3_matrix_url.err"
    message:
        "Creating " + PROJ + " sense 3_matrix url"
    threads:
        1
    run:
        with open(output[0], "w") as out:
            for file in input:
                for line in open(file, "r"):
                    out.write(line)




