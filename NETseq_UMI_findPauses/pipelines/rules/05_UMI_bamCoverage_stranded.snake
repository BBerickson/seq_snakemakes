# ====== Rules for making stranded bigwig with deeptools bamCoverage =================================

def _get_col(wildcards):
    sample = wildcards.sample
  
    if sample in COLS_DICT:
      results = (COLS_DICT[sample])
    else:
      results = "0,0,0"
    return(results)
          

# 5' or 3' bigwigs in R1-R2 orentation
rule bamCoverage:
    input:
        bam = PROJ + "/bams/{sample}_" + INDEX_SAMPLE + "_" + SEQ_DATE + "_dedup_subsample.bam",
        bai = PROJ + "/bams/{sample}_" + INDEX_SAMPLE + "_" + SEQ_DATE + "_dedup_subsample.bam.bai"
    output:
        fw  =   PROJ   + "/bw/{sample}_" + INDEX_SAMPLE + "_" + SEQ_DATE + "_" + NORMS + "_fw.bw",
        rev =   PROJ   + "/bw/{sample}_" + INDEX_SAMPLE + "_" + SEQ_DATE + "_" + NORMS + "_rev.bw",
        stats = temp(PROJ   + "/bw/{sample}_bamCoverage_spike_stats.txt")
    params:
        job_name = "{sample}_bamCoverage",
        memory   = MEMORY * 2,
        args     = CMD_PARAMS["bamCoverage"],
        color    = _get_col,
        url_fw   = "http://amc-sandbox.ucdenver.edu/" + USER + "/" + PROJ + "/{sample}_" + INDEX_SAMPLE + "_" + SEQ_DATE + "_" + NORMS + "_fw.bw",
        url_rev  = "http://amc-sandbox.ucdenver.edu/" + USER + "/" + PROJ + "/{sample}_" + INDEX_SAMPLE + "_" + SEQ_DATE + "_" + NORMS + "_rev.bw"
    log:
        out = PROJ + "/logs/{sample}_bamCoverage.out",
        err = PROJ + "/logs/{sample}_bamCoverage.err"
    message:
        "stranded bigwigs 5prim or 3prim reads in R1-R2 orentation for {wildcards.sample}"
    threads: 
        12
    shell:
        """
        # R1-R2 orentation
        bamCoverage \
          -b {input.bam} \
          -of bigwig \
          -o {output.rev} \
          {params.args} \
          -p 12 --filterRNAstrand forward

        bamCoverage \
          -b {input.bam} \
          -of bigwig \
          -o {output.fw} \
          {params.args} \
          -p 12 --filterRNAstrand reverse
        
        echo "track type=bigWig visibility=full name='{wildcards.sample}_{SEQ_DATE}_{NORMS}_fw' description='{wildcards.sample}_{SEQ_DATE}_{NORMS}_fw' color={params.color} bigDataUrl={params.url_fw}" > {output.stats}
        echo "track type=bigWig visibility=full name='{wildcards.sample}_{SEQ_DATE}_{NORMS}_rev' description='{wildcards.sample}_{SEQ_DATE}_{NORMS}_rev' color={params.color} bigDataUrl={params.url_rev}" >> {output.stats}
        ssh amc-sandbox 'mkdir -p ./public_html/{PROJ}'
        scp {output.fw} amc-sandbox:./public_html/{PROJ}
        scp {output.rev} amc-sandbox:./public_html/{PROJ}
        """


# Combine bamCoverage summaries
rule bamCoverage_summary:
    input:
        sorted(expand(
            PROJ + "/bw/{sample}_bamCoverage_spike_stats.txt",
            sample = SAMS_UNIQ
        ))
    output:
        PROJ + "/URLS/" + PROJ + "_" + INDEX_SAMPLE + "_" + NORMS + "_bw_URL.txt"
    params:
        job_name = PROJ + "_bamCoverage_summary",
        memory   = 4
    log:
        out = PROJ + "/logs/" + PROJ + "_bamCoverage_summary.out",
        err = PROJ + "/logs/" + PROJ + "_bamCoverage_summary.err"
    message:
        "Creating " + PROJ + " bamCoverage summary"
    threads:
        1
    run:
        with open(output[0], "w") as out:
            for file in input:
                for line in open(file, "r"):
                    out.write(line)


