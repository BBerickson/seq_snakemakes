# ===== Rules for running FastQC ===============================================


# Run fastqc
rule fastqc:
    input:
        FASTQ_DIR + "/{fastq}" + SFX
    output:
        PROJ + "/fastqc/{fastq}_fastqc.zip",
        PROJ + "/fastqc/{fastq}_fastqc.html"
    params:
        job_name = "{fastq}_fastqc",
        memory   = MEMORY * 5,
        out      = PROJ + "/fastqc"
    log:
        out = PROJ + "/logs/{fastq}_fastqc.out",
        err = PROJ + "/logs/{fastq}_fastqc.err"
    message:
        "Running FastQC on {wildcards.fastq}"
    threads:
        6
    shell:
        """
        mkdir -p {params.out}

        fastqc \
            -t {threads} \
            -f fastq \
            -o {params.out} \
            {input}
        """


# Combine FastQC summaries
rule fastqc_summary:
    input:
        expand(
            PROJ + "/fastqc/{fastq}_fastqc.zip",
            fastq = FASTQS
        )
    output:
        PROJ + "/stats/" + PROJ + "_fastqc.tsv"
    params:
        job_name = PROJ + "_fastqc_summary",
        memory   = MEMORY
    log:
        out = PROJ + "/logs/" + PROJ + "_fastqc_summary.out",
        err = PROJ + "/logs/" + PROJ + "_fastqc_summary.err"
    message:
        "Creating " + PROJ + " FastQC summary"
    threads:
        1
    shell:
        """
        for dir in {input};
        do
            name=$(basename -s .zip $dir)

            unzip -p $dir $name/summary.txt \
                >> {output}
        done
        """

