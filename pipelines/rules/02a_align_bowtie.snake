# ====== Rules for aligning reads =================================


# Align trimmed reads with bowtie2 PE
rule bowtiePE:
    input:
        R1  = PROJ + "/{sample}_R1.fastq.gz",
        R2  = PROJ + "/{sample}_R2.fastq.gz"
    output:
        bam   = temp(PROJ + "/{sample}_" + INDEX_MAP + ".bam"),
        bai   = temp(PROJ + "/{sample}_" + INDEX_MAP + ".bam.bai"),
        stats = PROJ + "/bams/{sample}_" + INDEX_MAP + "_bowtie_stats.txt"
    params:
        job_name = "{sample}_bowtiePE",
        idx      = INDEX_PATH + INDEX_MAP,
        args     = CMD_PARAMS["bowtie2"],
        args2    = CMD_PARAMS["samtools"],
        sortname = PROJ + "/{sample}.temp"
    resources:
        memory   = lambda wildcards, input: memory_estimator([input.R1, input.R2], 3, 18)
    log:
        out = PROJ + "/logs/{sample}_bowtie.out",
        err = PROJ + "/logs/{sample}_bowtie.err"
    message:
        "Aligning reads with Bowtie2 for {wildcards.sample}"
    threads: 
        12
    shell:
        """
        bowtie2 \
            -p {threads} \
            -x {params.idx} \
            -1 {input.R1} \
            -2 {input.R2} \
            {params.args} \
            2> {output.stats} \
            | samtools view {params.args2} - \
            | samtools sort - -T {params.sortname} -@ {threads} -O bam \
            > {output.bam}
        
        samtools index -@ {threads} {output.bam}
        
        """

# ====== Rules for aligning reads summmary =================================


# Align trimmed reads with bowtie2 PE summmary 
rule bowtiePE_summmary:
    input:
        expand(
            PROJ + "/bams/{sample}_" + INDEX_MAP + "_bowtie_stats.txt",
            sample = SAMS_UNIQ
        )
    output:
        PROJ + "/stats/" + PROJ + "_aligned.tsv"
    params:
        job_name = "bowtiePE_summmary"
    resources:
        memory   = 1
    log:
        out = PROJ + "/logs/bowtie_summmary.out",
        err = PROJ + "/logs/bowtie_summmary.err"
    message:
        "summmary Bowtie2 for"
    threads:
        1
    run:
        with open(output[0], "w") as out:
            for file in input:
                name = os.path.basename(file)
                name = re.sub("_" + INDEX_MAP + "_bowtie_stats.txt", "", name)

                for line in open(file, "r"):
                    match = re.search("overall alignment rate", line)

                    if match:
                        num = re.search("[0-9.%]+", line)
                        num = num.group(0).strip()
                        met = re.search("[a-z\s]+", line).group(0)
                        met = met.strip()
                        met = re.sub(" ", "_", met)

                        out.write("%s\t%s\t%s\n" % (name, met, num))
                    else:
                        line  = re.sub("; of these:", "", line.strip())
                        line  = re.sub(" \([0-9\.%]+\)", "", line)
                        words = line.split(" ")
                        num   = words[0]
                        met   = words[1:]
                        met   = " ".join(met)

                        out.write("%s\t%s\t%s\n" % (name, met, num))
                        

                    