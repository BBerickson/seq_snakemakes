# ====== Rules for aligning reads =================================


# Align trimmed reads with bowtie2 R1 or R2
rule bowtieR1:
    input:
        R1  = PROJ + "/{sample}_" + ORIENTATION + ".fastq.gz"
    output:
        bam   = temp(PROJ + "/{sample}_" + INDEX_MAP + ".bam"),
        bai   = temp(PROJ + "/{sample}_" + INDEX_MAP + ".bam.bai"),
        stats = PROJ + "/bams/{sample}_" + INDEX_MAP + "_bowtie_" + ORIENTATION + "_stats.txt"
    params:
        job_name = "{sample}_bowtie_unpaired",
        idx      = INDEX_PATH + INDEX_MAP,
        args     = CMD_PARAMS["bowtie2"],
        args2    = CMD_PARAMS["samtools"],
        sortname = PROJ + "/{sample}.temp"
    resources:
        memory   = lambda wildcards, input: memory_estimator(input, 3, 16)
    log:
        out = PROJ + "/logs/{sample}_bowtie_" + ORIENTATION + ".out",
        err = PROJ + "/logs/{sample}_bowtie_" + ORIENTATION + ".err"
    message:
        "Aligning reads with Bowtie2 for {wildcards.sample}"
    threads: 
        12
    shell:
        """
        bowtie2 \
            -p {threads} \
            -x {params.idx} \
            -U {input.R1} \
            {params.args} \
            2> {output.stats} \
            | samtools view {params.args2} - \
            | samtools sort - -T {params.sortname} -@ {threads} -O bam \
            > {output.bam}
        
        samtools index -@ {threads} {output.bam}
        """


# ====== Rules for aligning reads summmary =================================


# Align trimmed reads with bowtie2 summmary
rule bowtieR1_summmary:
    input:
        expand(
            PROJ + "/bams/{sample}_" + INDEX_MAP + "_bowtie_" + ORIENTATION + "_stats.txt",
            sample = SAMS_UNIQ
        )
    output:
        PROJ + "/stats/" + PROJ + "_aligned.tsv"
    params:
        job_name = "bowtie_unpaired_summmary"
    resources:
        memory   = 1
    log:
        out = PROJ + "/logs/bowtie_unpaired_summmary.out",
        err = PROJ + "/logs/bowtie_unparied_summmary.err"
    message:
        " unpaired summmary Bowtie2 "
    threads:
        1
    run:
        with open(output[0], "w") as out:
            for file in input:
                name = os.path.basename(file)
                name = re.sub("_" + INDEX_MAP + "_bowtie_" + ORIENTATION + "_stats.txt", "", name)

                for line in open(file, "r"):
                    match = re.search("overall alignment rate", line)

                    if match:
                        num = re.search("[0-9.%]+", line)
                        num = num.group(0).strip()
                        met = re.search("[a-z\s]+", line).group(0)
                        met = met.strip()
                        met = re.sub(" ", "_", met)

                        out.write("%s\t%s\t%s\n" % (name, met, num))
                    else:
                        line  = re.sub("; of these:", "", line.strip())
                        line  = re.sub(" \([0-9\.%]+\)", "", line)
                        words = line.split(" ")
                        num   = words[0]
                        met   = words[1:]
                        met   = " ".join(met)

                        out.write("%s\t%s\t%s\n" % (name, met, num))
                        
                        

