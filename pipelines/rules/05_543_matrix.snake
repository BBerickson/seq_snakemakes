# ====== Rules for making standard 543 matrix files =================================

# matrix files
rule standared543_matrix:
    input:
        bw  =   PROJ   + "/bw/{newnam}_" + INDEX_SAMPLE + "_" + SEQ_DATE + NORMS + ".bw"
    output:
        matrix = temp(PROJ   + "/matrix/{newnam}_temp0_543_matrix.gz")
    params:
        job_name = "{newnam}_matrix",
        args     = CMD_PARAMS["region543"]  + CMD_PARAMS["BL"],
        my_ref   = MY_REF
    resources:
        memory   = lambda wildcards, input: memory_estimator(input, 2, 5)
    log:
        out = PROJ + "/logs/{newnam}_matrix.out",
        err = PROJ + "/logs/{newnam}_matrix.err"
    message:
        "standard sense 543 matrix files {wildcards.newnam}"
    threads: 
        12
    shell:
        """
        # 
        computeMatrix scale-regions \
          {params.args} \
          -R {params.my_ref} \
          -S {input.bw} \
          -p {threads} \
          -o {output.matrix}

        """

# stranded sense matrix files
rule fix_543_matrix:
    input:
        PROJ   + "/matrix/{newnam}_temp0_543_matrix.gz"
    output:
        PROJ + "/matrix/543_{newnam}_" + INDEX_SAMPLE + "_" + SEQ_DATE + NORM543 + "_matrix.gz"
    params:
        job_name = "{newnam}_sense_matrix",
        args    = CMD_PARAMS["filter543"]
    resources:
        memory   = 4
    log:
        out = PROJ + "/logs/{newnam}_fix_543_matrix.out",
        err = PROJ + "/logs/{newnam}_fix_543_matrix.err"
    message:
        "fix standard 543 matrix files {wildcards.newnam}"
    threads: 
        1
    script:
        '../R_scripts/MatrixTrim.R'
        
# make url's and scp to sandbox
rule standared543_url:
    input:
        PROJ + "/matrix/543_{newnam}_" + INDEX_SAMPLE + "_" + SEQ_DATE + NORM543 + "_matrix.gz"
    output:
        temp(PROJ + "/matrix/543.{newnam}_matrix.url.txt")
    params:
        job_name = PROJ + "_543_matrix_url",
        color    = _get_col,
        url_out  = "http://amc-sandbox.ucdenver.edu/" + USER + "/" + SEQ_DATE + "_" + PROJ + "/543_{newnam}_" + INDEX_SAMPLE + "_" + SEQ_DATE + NORM543 + "_matrix.gz"
    resources:
        memory   = 4
    log:
        out = PROJ + "/logs/{newnam}_543_matrix_url.out",
        err = PROJ + "/logs/{newnam}_543_matrix_url.err"
    message:
        "Creating sense 543_matrix url for {wildcards.newnam}"
    threads:
        1
    shell:
        """
        
        # fix R appended file remote read error
        computeMatrixOperations relabel -m {input} -o {input}
        echo {params.url_out} self 543.{wildcards.newnam}{NORMS}_{SEQ_DATE} {params.color} >> {output[0]}
        ssh amc-sandbox 'mkdir -p ./public_html/{SEQ_DATE}_{PROJ}'
        scp {input[0]} amc-sandbox:./public_html/{SEQ_DATE}_{PROJ}
        """
        
# Combine urls
rule sense_url_summary:
    input:
        sorted(expand(
            PROJ + "/matrix/543.{newnam}_matrix.url.txt",
            newnam = NAMS_UNIQ
        ))
    output:
        PROJ + "/URLS/543_" + PROJ + "_" + INDEX_SAMPLE + NORM543 + "_matrix.url.txt"
    params:
        job_name = PROJ + "_543_matrix_url"
    resources:
        memory   = 4
    log:
        out = PROJ + "/logs/" + PROJ + "_543_matrix_url.out",
        err = PROJ + "/logs/" + PROJ + "_543_matrix_url.err"
    message:
        "Creating " + PROJ + " sense 543_matrix url"
    threads:
        1
    run:
        with open(output[0], "w") as out:
            for file in input:
                for line in open(file, "r"):
                    out.write(line)




