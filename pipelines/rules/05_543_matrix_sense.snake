# ====== Rules for making standard sense 543 matrix files =================================


# stranded sense matrix files
rule sense_543_matrix:
    input:
        fw  =   PROJ   + "/bw/{group}_" + INDEX_SAMPLE + "_" + SEQ_DATE + "_" + NORMS + "_fw.bw",
        rev =   PROJ   + "/bw/{group}_" + INDEX_SAMPLE + "_" + SEQ_DATE + "_" + NORMS + "_rev.bw"
    output:
        sense0  = temp(PROJ   + "/matrix/{group}_temp0_543_sense_matrix.gz"),
        sense1  = temp(PROJ   + "/matrix/{group}_temp1_543_sense_matrix.gz"),
        sense2  = temp(PROJ   + "/matrix/{group}_temp2_543_sense_matrix.gz")
    params:
        job_name = "{group}_sense_matrix",
        memory   = MEMORY * 5,
        args     = CMD_PARAMS["region543"]  + CMD_PARAMS["BL"],
        fw_ref   = FW_REF,
        rev_ref  = REV_REF,
        color    = _get_col
    log:
        out = PROJ + "/logs/{group}_543_sense_matrix.out",
        err = PROJ + "/logs/{group}_543_sense_matrix.err"
    message:
        "standard sense 543 matrix files {wildcards.group}"
    threads: 
        12
    shell:
        """
        # no unscaled body
        computeMatrix scale-regions \
          {params.args} \
          -R {params.fw_ref} \
          -S {input.fw} \
          -p {threads} \
          -o {output.sense1}

        computeMatrix scale-regions \
          {params.args} \
          -R {params.rev_ref} \
          -S {input.rev} \
          -p {threads} \
          -o {output.sense2}

        computeMatrixOperations rbind -m {output.sense1} {output.sense2} -o {output.sense0}

        """

# stranded sense matrix files
rule fix_543_sense_matrix:
    input:
        PROJ   + "/matrix/{group}_temp0_543_sense_matrix.gz"
    output:
        temp(PROJ + "/matrix/{group}_temp0_fix_543_sense_matrix.gz")
    params:
        job_name = "{group}_sense_matrix",
        memory   = MEMORY,
        args    = CMD_PARAMS["filter543"]
    log:
        out = PROJ + "/logs/{group}_fix_543_sense_matrix.out",
        err = PROJ + "/logs/{group}_fix_543_sense_matrix.err"
    message:
        "fix standard sense 543 matrix files {wildcards.group}"
    threads: 
        1
    script:
        '../R_scripts/MatrixTrim.R'
        
# stranded sense matrix files
rule urls_543_sense_matrix:
    input:
        PROJ + "/matrix/{group}_temp0_fix_543_sense_matrix.gz"
    output:
        temp(PROJ + "/matrix/543.{group}_matrix.url.txt"),
        PROJ   + "/matrix/543_{group}_" + INDEX_SAMPLE + "_" + SEQ_DATE + "_" + NORM543 + "_matrix.gz"
    params:
        job_name = "{group}_sense_matrix",
        memory   = 4,
        color    = _get_col,
        url_out  = "http://amc-sandbox.ucdenver.edu/" + USER + "/" + SEQ_DATE + "_" + PROJ + "/543_{group}_" + INDEX_SAMPLE + "_" + SEQ_DATE + "_" + NORM543 + "_matrix.gz"
    log:
        out = PROJ + "/logs/{group}_url_543_sense_matrix.out",
        err = PROJ + "/logs/{group}_url_543_sense_matrix.err"
    message:
        "url standard sense 543 matrix files {wildcards.group}"
    threads: 
        1
    shell:
        """
        # fix R appended file remote read error
        computeMatrixOperations relabel -m {input} -o {output[1]}
        echo {params.url_out} 543 543.{wildcards.group}_{SEQ_DATE} {params.color} >> {output[0]}
        ssh amc-sandbox 'mkdir -p ./public_html/{SEQ_DATE}_{PROJ}'
        scp {output[1]} amc-sandbox:./public_html/{SEQ_DATE}_{PROJ}

        """
                
# Combine urls
rule sense_543_url_summary:
    input:
        sorted(expand(
            PROJ + "/matrix/543.{group}_matrix.url.txt",
            group = GRPS_UNIQ
        ))
    output:
        PROJ + "/URLS/543_" + PROJ + "_" + INDEX_SAMPLE + "_" + NORM543 + "_sense_matrix.url.txt"
    params:
        job_name = PROJ + "_sense_543_matrix_url",
        memory   = 4
    log:
        out = PROJ + "/logs/" + PROJ + "_sense_543_matrix_url.out",
        err = PROJ + "/logs/" + PROJ + "_sense_543_matrix_url.err"
    message:
        "Creating " + PROJ + " sense 543_matrix url"
    threads:
        1
    run:
        with open(output[0], "w") as out:
            for file in input:
                for line in open(file, "r"):
                    out.write(line)




