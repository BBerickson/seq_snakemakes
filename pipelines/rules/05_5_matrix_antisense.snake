# ====== Rules for making standard sense 5 matrix files =================================


# stranded antisense matrix files
rule my_5_amtisense_matrix:
    input:
        fw  =   PROJ   + "/bw/{newnam}_" + INDEX_SAMPLE + "_" + SEQ_DATE + NORMS + "_fw.bw",
        rev =   PROJ   + "/bw/{newnam}_" + INDEX_SAMPLE + "_" + SEQ_DATE + NORMS + "_rev.bw"
    output:
        asense   = temp(PROJ   + "/matrix/{newnam}_temp0_a5_matrix.gz"),
        asense1  = temp(PROJ   + "/matrix/{newnam}_temp1_a5_matrix.gz"),
        asense2  = temp(PROJ   + "/matrix/{newnam}_temp2_a5_matrix.gz")
    params:
        job_name = "{newnam}_5_antisense_matrix",
        args     = CMD_PARAMS["reference5"] + CMD_PARAMS["bamCoverageBL"],
        fw_ref   = FW_REF,
        rev_ref  = REV_REF
    resources:
        memory   = lambda wildcards, input: memory_estimator(input, 2, 5)
    log:
        out = PROJ + "/logs/{newnam}_antisense_5_matrix.out",
        err = PROJ + "/logs/{newnam}_antisense_5_matrix.err"
    message:
        "standard antisense 5 matrix files {wildcards.newnam}"
    threads: 
        12
    shell:
        """
        # no unscaled body
        computeMatrix reference-point \
          {params.args} \
          --scale -1 \
          -R {params.fw_ref} \
          -S {input.rev} \
          -p {threads} \
          -o {output.asense1}

        computeMatrix reference-point \
          {params.args} \
          --scale -1 \
          -R {params.rev_ref} \
          -S {input.fw} \
          -p {threads} \
          -o {output.asense2}

        computeMatrixOperations rbind -m {output.asense1} {output.asense2} -o {output.asense}

        """

# stranded asense matrix files
rule fix_5_asense_matrix:
    input:
        PROJ   + "/matrix/{newnam}_temp0_a5_matrix.gz"
    output:
        temp(PROJ   + "/matrix/{newnam}_temp0_fix_a5_matrix.gz")
    params:
        job_name = "{newnam}_5_asense_matrix",
        args    = CMD_PARAMS["reference5"] + " unstranded"
    resources:
        memory   = 4
    log:
        out = PROJ + "/logs/{newnam}_fix_5_asense_matrix.out",
        err = PROJ + "/logs/{newnam}_fix_5_asense_matrix.err"
    message:
        "fix standard asense 5 matrix files {wildcards.newnam}"
    threads: 
        1
    script:
        '../R_scripts/MatrixTrim.R'
        
# make url's and scp to sandbox
rule my_5_url_a:
    input:
        PROJ   + "/matrix/{newnam}_temp0_fix_a5_matrix.gz"
    output:
        temp(PROJ + "/matrix/5.{newnam}_matrixa.url.txt"),
        PROJ + "/matrix/5_{newnam}_" + INDEX_SAMPLE + "_" + SEQ_DATE + NORM5[1] + "_anti_matrix.gz"
    params:
        job_name = PROJ + "_5_matrix_anti_url",
        color    = _get_col,
        groupkey = lambda wildcards: next((k for k, v in GROUP2.items() if wildcards.newnam in v), "self"),
        url_anti = "http://amc-sandbox.ucdenver.edu/" + USER + "/" + SEQ_DATE + "_" + PROJ + "/5_{newnam}_" + INDEX_SAMPLE + "_" + SEQ_DATE + NORM5[1] + "_anti_matrix.gz"
    resources:
        memory   = 4
    log:
        out = PROJ + "/logs/{newnam}_sense_5_matrix_url.out",
        err = PROJ + "/logs/{newnam}_sense_5_matrix_url.err"
    message:
        "Creating sense 5_matrix url for {wildcards.newnam}"
    threads:
        1
    shell:
        """
        # fix R appended file remote read error
        computeMatrixOperations relabel -m {input} -o {output[1]}
        ssh amc-sandbox 'mkdir -p ./public_html/{SEQ_DATE}_{PROJ}'
        echo {params.url_anti} {params.groupkey} 5.{wildcards.newnam}_{SEQ_DATE}_anti {params.color} >> {output[0]}
        scp {output[1]} amc-sandbox:./public_html/{SEQ_DATE}_{PROJ}
        
        """
        
# Combine urls
rule my_5_url_a_summary:
    input:
        sorted(expand(
            PROJ + "/matrix/5.{newnam}_matrixa.url.txt",
            newnam = NAMS_UNIQ
        ))
    output:
        PROJ + "/URLS/5_" + PROJ + "_" + INDEX_SAMPLE + NORM5[1] + "_antisense_matrix.url.txt"
    params:
        job_name = PROJ + "_asense_5_matrix_url"
    resources:
        memory   = 1
    log:
        out = PROJ + "/logs/" + PROJ + "_asense_5_matrix_url.out",
        err = PROJ + "/logs/" + PROJ + "_asense_5_matrix_url.err"
    message:
        "Creating " + PROJ + " antisense 5_matrix url"
    threads:
        1
    run:
        with open(output[0], "w") as out:
            for file in input:
                for line in open(file, "r"):
                    out.write(line)




