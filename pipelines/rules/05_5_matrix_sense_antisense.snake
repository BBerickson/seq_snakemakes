# ====== Rules for making standard sense 5 matrix files =================================

# stranded sense matrix files
rule my_5_sense_matrix2:
    input:
        fw  =   PROJ   + "/bw/{newnam}_" + INDEX_SAMPLE + "_" + SEQ_DATE + NORMS + "_fw.bw",
        rev =   PROJ   + "/bw/{newnam}_" + INDEX_SAMPLE + "_" + SEQ_DATE + NORMS + "_rev.bw"
    output:
        sense   = temp(PROJ   + "/matrix/{newnam}_temp02_5_matrix.gz"),
        sense1  = temp(PROJ   + "/matrix/{newnam}_temp12_5_matrix.gz"),
        sense2  = temp(PROJ   + "/matrix/{newnam}_temp22_5_matrix.gz")
    params:
        job_name = "{newnam}_sense_matrix2",
        args     = CMD_PARAMS["reference5"]  + CMD_PARAMS["bamCoverageBL"],
        fw_ref   = FW_REF,
        rev_ref  = REV_REF
    resources:
        memory   = MEMORY * 5
    log:
        out = PROJ + "/logs/{newnam}_sense_5_matrix2.out",
        err = PROJ + "/logs/{newnam}_sense_5_matrix2.err"
    message:
        "standard sense 5 matrix files {wildcards.newnam}"
    threads: 
        12
    shell:
        """
        # no unscaled body
        computeMatrix reference-point \
          {params.args} \
          -R {params.fw_ref} \
          -S {input.fw} \
          -p {threads} \
          -o {output.sense1}

        computeMatrix reference-point \
          {params.args} \
          -R {params.rev_ref} \
          -S {input.rev} \
          -p {threads} \
          -o {output.sense2}

        computeMatrixOperations rbind -m {output.sense1} {output.sense2} -o {output.sense}

        """

# stranded antisense matrix files
rule my_5_amtisense_matrix2:
    input:
        fw  =   PROJ   + "/bw/{newnam}_" + INDEX_SAMPLE + "_" + SEQ_DATE + NORMS + "_fw.bw",
        rev =   PROJ   + "/bw/{newnam}_" + INDEX_SAMPLE + "_" + SEQ_DATE + NORMS + "_rev.bw"
    output:
        asense   = temp(PROJ   + "/matrix/{newnam}_temp02_a5_matrix.gz"),
        asense1  = temp(PROJ   + "/matrix/{newnam}_temp12_a5_matrix.gz"),
        asense2  = temp(PROJ   + "/matrix/{newnam}_temp22_a5_matrix.gz")
    params:
        job_name = "{newnam}_5_antisense_matrix2",
        args     = CMD_PARAMS["reference5"] + CMD_PARAMS["bamCoverageBL"],
        fw_ref   = FW_REF,
        rev_ref  = REV_REF
    resources:
        memory   = lambda wildcards, input: memory_estimator(input, 2, 5)
    log:
        out = PROJ + "/logs/{newnam}_antisense_5_matrix2.out",
        err = PROJ + "/logs/{newnam}_antisense_5_matrix2.err"
    message:
        "standard antisense 5 matrix files {wildcards.newnam}"
    threads: 
        12
    shell:
        """
        # no unscaled body
        computeMatrix reference-point \
          {params.args} \
          --scale -1 \
          -R {params.fw_ref} \
          -S {input.rev} \
          -p {threads} \
          -o {output.asense1}

        computeMatrix reference-point \
          {params.args} \
          --scale -1 \
          -R {params.rev_ref} \
          -S {input.fw} \
          -p {threads} \
          -o {output.asense2}

        computeMatrixOperations rbind -m {output.asense1} {output.asense2} -o {output.asense}

        """

# stranded sense matrix files
rule fix_5_sense_matrix2:
    input:
        PROJ   + "/matrix/{newnam}_temp02_5_matrix.gz"
    output:
        temp(PROJ   + "/matrix/{newnam}_temp02_fix_5_matrix.gz")
    params:
        job_name = "{newnam}_5_sense_matrix2",
        args    = CMD_PARAMS["reference5"] + " stranded"
    resources:
        memory   = 4
    log:
        out = PROJ + "/logs/{newnam}_fix_5_sense_matrix.out",
        err = PROJ + "/logs/{newnam}_fix_5_sense_matrix.err"
    message:
        "fix standard sense 5 matrix files {wildcards.newnam}"
    threads: 
        1
    script:
        '../R_scripts/MatrixTrim.R'
        

# stranded asense matrix files
rule fix_5_asense_matrix2:
    input:
        PROJ   + "/matrix/{newnam}_temp02_a5_matrix.gz"
    output:
        temp(PROJ   + "/matrix/{newnam}_temp02_fix_a5_matrix.gz")
    params:
        job_name = "{newnam}_5_asense_matrix2",
        args    = CMD_PARAMS["reference5"] + " unstranded"
    resources:
        memory   = 4
    log:
        out = PROJ + "/logs/{newnam}_fix_5_asense_matrix2.out",
        err = PROJ + "/logs/{newnam}_fix_5_asense_matrix2.err"
    message:
        "fix standard asense 5 matrix files {wildcards.newnam}"
    threads: 
        1
    script:
        '../R_scripts/MatrixTrim.R'
        
# make url's and scp to sandbox
rule my_5_url:
    input:
        matrix1 = PROJ   + "/matrix/{newnam}_temp02_fix_5_matrix.gz",
        matrix2 = PROJ   + "/matrix/{newnam}_temp02_fix_a5_matrix.gz"
    output:
        temp(PROJ + "/matrix/5.{newnam}_matrix.url.txt"),
        PROJ + "/matrix/5_{newnam}_" + INDEX_SAMPLE + "_" + SEQ_DATE + NORM5[1] + "_matrix.gz",
        PROJ + "/matrix/5_{newnam}_" + INDEX_SAMPLE + "_" + SEQ_DATE + NORM5[1] + "_anti_matrix.gz"
    params:
        job_name = PROJ + "_5_matrix_url2",
        color    = _get_col,
        groupkey = lambda wildcards: next((k for k, v in GROUP2.items() if wildcards.newnam in v), "self"),
        url_out  = "http://amc-sandbox.ucdenver.edu/" + USER + "/" + PROJ + "/5_{newnam}_" + INDEX_SAMPLE + "_" + SEQ_DATE + NORM5[1] + "_matrix.gz",
        url_anti = "http://amc-sandbox.ucdenver.edu/" + USER + "/" + PROJ + "/5_{newnam}_" + INDEX_SAMPLE + "_" + SEQ_DATE + NORM5[1] + "_anti_matrix.gz"
    resources:
        memory   = 4
    log:
        out = PROJ + "/logs/{newnam}_sense_5_matrix_url2.out",
        err = PROJ + "/logs/{newnam}_sense_5_matrix_url2.err"
    message:
        "Creating sense 5_matrix url for {wildcards.newnam}"
    threads:
        1
    shell:
        """
        # fix for strange side affect that only the 1 line loads from sandbox
        computeMatrixOperations relabel -m {input.matrix1} -o {output[1]}
        computeMatrixOperations relabel -m {input.matrix2} -o {output[2]}
        echo {params.url_out} {params.groupkey} 5.{wildcards.newnam}_{SEQ_DATE} {params.color} >> {output[0]}
        ssh amc-sandbox 'mkdir -p ./public_html/{PROJ}'
        scp {output[1]} amc-sandbox:./public_html/{PROJ}
        echo {params.url_anti} {params.groupkey} 5.{wildcards.newnam}_{SEQ_DATE}_anti {params.color} >> {output[0]}
        scp {output[2]} amc-sandbox:./public_html/{PROJ}
        """
        
# Combine urls
rule my_5_url_summary:
    input:
        sorted(expand(
            PROJ + "/matrix/5.{newnam}_matrix.url.txt",
            newnam = NAMS_UNIQ
        ))
    output:
        PROJ + "/URLS/5_" + PROJ + "_" + INDEX_SAMPLE + NORM5[1] + "_matrix.url.txt"
    params:
        job_name = PROJ + "_sense_5_matrix_url"
    resources:
        memory   = 4
    log:
        out = PROJ + "/logs/" + PROJ + "_sense_5_matrix_url.out",
        err = PROJ + "/logs/" + PROJ + "_sense_5_matrix_url.err"
    message:
        "Creating " + PROJ + " sense 5_matrix url"
    threads:
        1
    run:
        with open(output[0], "w") as out:
            for file in input:
                for line in open(file, "r"):
                    out.write(line)




