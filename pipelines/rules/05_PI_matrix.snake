# ====== Rules for making standard sense PI matrix files =================================

# stranded sense matrix files
rule my_PI_matrix:
    input:
        bw  =   PROJ   + "/bw/{newnam}_" + INDEX_SAMPLE + "_" + SEQ_DATE + "_" + NORMS + ".bw"
    output:
        matrix = temp(PROJ   + "/matrix/{newnam}_temp0_PI_matrix.gz")
    params:
        job_name = "{newnam}_matrix",
        args     = CMD_PARAMS["PI"]  + CMD_PARAMS["BL"],
        my_ref   = PI_REF
    resources:
        memory   = MEMORY * 3
    log:
        out = PROJ + "/logs/{newnam}_PI_matrix.out",
        err = PROJ + "/logs/{newnam}_PI_matrix.err"
    message:
        "PI matrix files {wildcards.newnam}"
    threads: 
        12
    shell:
        """
        # 
        computeMatrix scale-regions \
          {params.args} \
          -R {params.my_ref} \
          -S {input.bw} \
          -p {threads} \
          -o {output.matrix}

        """

# stranded sense matrix files
rule fix_PI_matrix:
    input:
        PROJ   + "/matrix/{newnam}_temp0_PI_matrix.gz"
    output:
        PROJ + "/matrix/PI_{newnam}_" + INDEX_SAMPLE + "_" + SEQ_DATE + "_" + NORMPI + "_matrix.gz"
    params:
        job_name = "{newnam}_sense_matrix",
        args     = CMD_PARAMS["filterPI"]
    resources:
        memory   = 4
    log:
        out = PROJ + "/logs/{newnam}_fix_PI_matrix.out",
        err = PROJ + "/logs/{newnam}_fix_PI_matrix.err"
    message:
        "fix standard PI matrix files {wildcards.newnam}"
    threads: 
        1
    script:
        '../R_scripts/MatrixTrim.R'
        

# make url's and scp to sandbox
rule my_PI_url:
    input:
        PROJ + "/matrix/PI_{newnam}_" + INDEX_SAMPLE + "_" + SEQ_DATE + "_" + NORMPI + "_matrix.gz"
    output:
        temp(PROJ + "/matrix/PI.{newnam}_matrix.url.txt")
    params:
        job_name = PROJ + "_PI_matrix_url",
        color    = _get_col,
        url_out  = "http://amc-sandbox.ucdenver.edu/" + USER + "/" + SEQ_DATE + "_" + PROJ + "/PI_{newnam}_" + INDEX_SAMPLE + "_" + SEQ_DATE + "_" + NORMPI + "_matrix.gz"
    resources:
        memory   = MEMORY
    log:
        out = PROJ + "/logs/{newnam}_PI_matrix_url.out",
        err = PROJ + "/logs/{newnam}_PI_matrix_url.err"
    message:
        "Creating PI_matrix url for {wildcards.newnam}"
    threads:
        1
    shell:
        """
        # fix R appended file remote read error
        computeMatrixOperations relabel -m {input} -o {input}
        echo {params.url_out} PI PI.{wildcards.newnam}_{NORMS}_{SEQ_DATE} {params.color} >> {output[0]}
        ssh amc-sandbox 'mkdir -p ./public_html/{SEQ_DATE}_{PROJ}'
        scp {input[0]} amc-sandbox:./public_html/{SEQ_DATE}_{PROJ}
        
        """
        
# Combine urls
rule my_PI_url_summary:
    input:
        sorted(expand(
            PROJ + "/matrix/PI.{newnam}_matrix.url.txt",
            newnam = NAMS_UNIQ
        ))
    output:
        PROJ + "/URLS/PI_" + PROJ + "_" + INDEX_SAMPLE + "_" + NORMPI + "_matrix.url.txt"
    params:
        job_name = PROJ + "_PI_matrix_url"
    resources:
        memory   = 4
    log:
        out = PROJ + "/logs/" + PROJ + "_PI_matrix_url.out",
        err = PROJ + "/logs/" + PROJ + "_PI_matrix_url.err"
    message:
        "Creating " + PROJ + " sense PI_matrix url"
    threads:
        1
    run:
        with open(output[0], "w") as out:
            for file in input:
                for line in open(file, "r"):
                    out.write(line)




