# ====== Rules for making standard matrix files =================================


# stranded sense matrix files
rule create_strand_matrix:
    input:
        fw  = PROJ + "/bw/{newnam}_" + INDEX_SAMPLE + "_" + SEQ_DATE + NORMS + "_fw.bw",
        rev = PROJ + "/bw/{newnam}_" + INDEX_SAMPLE + "_" + SEQ_DATE + NORMS + "_rev.bw"
    output:
        matrix   = PROJ + "/matrix/{region}/{newnam}_" + INDEX_SAMPLE + "_" + SEQ_DATE + "_{region}_{covarg}_{sense_asense}_matrix.gz",
        fw       = temp(PROJ + "/matrix/{newnam}_{sense_asense}_temp1_{region}_{covarg}_{sense_asense}_matrix.gz"),
        rev      = temp(PROJ + "/matrix/{newnam}_{sense_asense}_temp2_{region}_{covarg}_{sense_asense}_matrix.gz")
    params:
        job_name = "{region}_{covarg}_{newnam}_{sense_asense}_matrix",
        args = lambda wildcards: (
            CMD_PARAMS[f"region{wildcards.region}"] + CMD_PARAMS["bamCoverageBL"] +
            (" --scale -1 " if wildcards.sense_asense == "anti" else "")
        ),
        args2    = "stranded",
        fw_ref   = lambda wildcards: FW_REF if wildcards.sense_asense == "sense" else REV_REF,
        rev_ref  = lambda wildcards: REV_REF if wildcards.sense_asense == "sense" else FW_REF
    resources:
        memory   = lambda wildcards, input: memory_estimator(input, 2, 5)
    log:
        out = PROJ + "/logs/{newnam}_{region}_{covarg}_{sense_asense}_matrix.out",
        err = PROJ + "/logs/{newnam}_{region}_{covarg}_{sense_asense}_matrix.err"
    threads: 
        12
    shell:
        """
        computeMatrix {params.args} -R {params.fw_ref} -S {input.fw} -p {threads} -o {output.fw} 2>> {log.err}
        computeMatrix {params.args} -R {params.rev_ref} -S {input.rev} -p {threads} -o {output.rev} 2>> {log.err}
        computeMatrixOperations rbind -m {output.fw} {output.rev} -o {output.matrix} 2>> {log.err}
        
        """

        
# make url's and scp to sandbox
rule standared_marix_url:
    input:
        PROJ + "/matrix/{region}/{newnam}_" + INDEX_SAMPLE + "_" + SEQ_DATE + "_{region}_{covarg}_{sense_asense}_matrix.gz"
    output:
        temp(PROJ + "/matrix/{region}/{newnam}_{region}_{covarg}_{sense_asense}_matrix.url.txt")
    params:
        job_name = "{newnam}_{region}_{covarg}_{sense_asense}_matrix_url",
        color    = lambda wildcards: _get_col(wildcards.newnam, COLS_DICT),
        groupkey = lambda wildcards: next((k for k, v in GROUPS.items() if wildcards.newnam in v), "self"),
        url_out  = "http://amc-sandbox.ucdenver.edu/" + USER + "/" + SEQ_DATE + "_" + PROJ + "/{newnam}_" + INDEX_SAMPLE + "_" + SEQ_DATE + "_{region}_{covarg}_{sense_asense}_matrix.gz"
    resources:
        memory   = 4
    log:
        out = PROJ + "/logs/{newnam}_{region}_{covarg}_{sense_asense}_matrix_url.out",
        err = PROJ + "/logs/{newnam}_{region}_{covarg}_{sense_asense}_matrix_url.err"
    threads:
        1
    shell:
        """
        echo {params.url_out} {params.groupkey} {wildcards.newnam}{NORMS}_{SEQ_DATE}_{wildcards.sense_asense} {params.color} >> {output[0]}
        ssh amc-sandbox 'mkdir -p ./public_html/{SEQ_DATE}_{PROJ}'
        scp {input[0]} amc-sandbox:./public_html/{SEQ_DATE}_{PROJ}
        """
        
# Combine URLs
rule matrix_url_summary:
    input:
        sorted(expand(
            PROJ + "/matrix/{region}/{newnam}_{region}_{covarg}_{sense_asense}_matrix.url.txt",
            zip, newnam=REGIONS_COVARGS['Newnam'], region=REGIONS_COVARGS['Region'], 
            covarg=REGIONS_COVARGS['Value'], sense_asense=REGIONS_COVARGS['Sense_Asense']
        ))
    output:
        PROJ + "/URLS/{region}_" + INDEX_SAMPLE + "_" + SEQ_DATE + "_{covarg}_{sense_asense}_matrix.url.txt"
    params:
        job_name = "{region}_{covarg}_{sense_asense}_matrix_url_summary"
    resources:
        memory = 4
    log:
        out = PROJ + "/logs/{region}_{covarg}_{sense_asense}_matrix_url_summary.out",
        err = PROJ + "/logs/{region}_{covarg}_{sense_asense}_matrix_url_summary.err"
    threads:
        1
    shell:
        """
        # Filter and concatenate files
        for file in {input}; do
            if [[ $file == *"{wildcards.region}"* && $file == *"{wildcards.covarg}"* && $file == *"{wildcards.sense_asense}"* ]]; then
                cat $file >> {output}
            fi
        done
        """



