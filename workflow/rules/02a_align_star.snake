# ====== Rules for aligning reads =================================


# Align trimmed reads with star
rule star_twopass:
    input:
        R1 = lambda wildcards: PROJ + f"/{wildcards.sample}_R1.fastq" + (".gz" if config.get("compress_temp", True) else ""),
        R2 = lambda wildcards: PROJ + f"/{wildcards.sample}_R2.fastq" + (".gz" if config.get("compress_temp", True) else "")
    output:
        bam   = temp(PROJ + "/{sample}_" + INDEX_MAP + ".bam"),
        bai   = temp(PROJ + "/{sample}_" + INDEX_MAP + ".bam.bai"),
        stats = PROJ + "/bams/{sample}_Log.out",
        final = PROJ + "/bams/{sample}_Log.final.out"
    params:
        job_name = "{sample}_star_twopass",
        gtf      = config_indexes[INDEXES[0]].get("GTF"),
        bam_unsorted = PROJ + "/bams/{sample}_Aligned.out.bam",
        args     = lambda wildcards: CMD_PARAMS["star"]["paired" if PAIREDMAP[wildcards.sample] else "single"],
        out      = PROJ + "/bams/{sample}_",
        read_cmd = "--readFilesCommand gunzip -c" if config.get("compress_temp", True) else ""
    resources:
        memory = lambda wildcards, input: memory_estimator([input.R1, input.R2], 3, 40, 250)
    log:
        out = PROJ + "/logs/{sample}_star_twopass.out",
        err = PROJ + "/logs/{sample}_star_twopass.err"
    message:
        "Running STAR alignment with two-pass mode"
    threads: 
        16
    shell:
        """
        # Use 80% of allocated memory for samtools, split across threads
        mem_per_thread=$(( {resources.memory} * 1024 * 80 / 100 / {threads} ))
        
        STAR \
          --genomeDir {INDEX_PATH} \
          --runThreadN {threads} \
          --readFilesIn {input.R1} {input.R2} \
          --outSAMtype BAM Unsorted \
          {params.read_cmd} \
          --outFileNamePrefix {params.out} \
          --sjdbGTFfile {params.gtf} \
          {params.args} \
          --twopassMode Basic
        
        samtools sort {params.bam_unsorted} \
            -T {params.out}tmp \
            -@ {threads} \
            -m "${{mem_per_thread}}M" \
            -o {output.bam}
            
        samtools index -@ {threads} {output.bam}
        
        rm {params.bam_unsorted}
        """

        

# ====== Rules for aligning reads summary =================================


rule star_2pass_summary:
    input:
        expand(
            PROJ + "/bams/{sample}_Log.final.out",
            sample=SAMS_UNIQ
        )
    output:
        PROJ + "/stats/" + PROJ + "_aligned.tsv"
    params:
        job_name = "star_2pass_summary",
        index = INDEX_MAP
    resources:
        memory = lambda wildcards, input: memory_estimator(input, 1, 1) # multiplier, min, max, unit=GB
    log:
        out=PROJ + "/logs/star_summary.out",
        err=PROJ + "/logs/star_summary.err"
    message:
        "Summary of STAR alignments"
    threads:
        4
    shell:
        """
        python - << 'EOF'
import sys
sys.path.insert(0, "workflow/scripts/")
import rules
        
rules._extract_star_log_info("{input}".split(), "{output}", "{params.index}")
EOF
        """
                        


                    